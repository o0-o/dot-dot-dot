" o0-o Airline Theme:
" Helpers: {{{

" Takes a foreground color name, background color name, and optionally one or
" more attr-list items as input, transforms it to the format accepted by
" airline#themes#generate_color_map and returns that value
func! s:clr(fg, bg, ...)
  let l:fg = g:o#palette[a:fg]
  let l:bg = g:o#palette[a:bg]
  return [ l:fg[0], l:bg[0], l:fg[1], l:bg[1] ] +
       \ filter(copy(a:000), 'type(v:val) == 1 && len(v:val) > 0')
endfunc

" Takes three ['fg', 'bg'] color lists and optionally a dictionary of extra
" key-value pairs and returns the value generated by
" airline#themes#generate_color_map after optionally merging the dictionary of
" extra key-value pairs.
"
" a:a -> airline_a, airline_x
" a:b -> airline_b, airline_y
" a:c -> airline_c, airline_z
func! s:color_map(a, b, c, ...)
  if a:0 == 0
    return call('airline#themes#generate_color_map', [call('s:clr', a:a), call('s:clr', a:b), call('s:clr', a:c)])
  else
    return call('extend', [ call('airline#themes#generate_color_map', [call('s:clr', a:a), call('s:clr', a:b), call('s:clr', a:c)]) ] + a:000)
  endif
endfunc

"}}}

let g:airline#themes#o#palette = {
\   'normal': s:color_map(
\       ['bg', 'brightblue'],
\       ['white', 'brightblack'],
\       ['white', 'bg'],
\       {
\         'airline_warning': s:clr('bg', 'yellow'),
\         'airline_error': s:clr('bg', 'red'),
\       },
\     ),
\   'normal_modified': s:color_map(
\       ['bg', 'cyan'],
\       ['white', 'brightblack'],
\       ['white', 'bg'],
\       {
\         'airline_warning': s:clr('bg', 'yellow'),
\         'airline_error': s:clr('bg', 'red'),
\       },
\     ),
\   'insert': s:color_map(
\       ['bg', 'green'],
\       ['white', 'brightblack'],
\       ['white', 'bg'],
\       {
\         'airline_warning': s:clr('bg', 'yellow'),
\         'airline_error': s:clr('bg', 'red'),
\       },
\     ),
\   'insert_modified': s:color_map(
\       ['bg', 'green'],
\       ['white', 'brightblack'],
\       ['white', 'bg'],
\       {
\         'airline_warning': s:clr('bg', 'yellow'),
\         'airline_error': s:clr('bg', 'red'),
\       },
\     ),
\   'replace': s:color_map(
\       ['bg', 'yellow'],
\       ['white', 'brightblack'],
\       ['white', 'bg'],
\       {
\         'airline_warning': s:clr('bg', 'yellow'),
\         'airline_error': s:clr('bg', 'red'),
\       },
\     ),
\   'replace_modified': s:color_map(
\       ['bg', 'yellow'],
\       ['white', 'brightblack'],
\       ['white', 'bg'],
\       {
\         'airline_warning': s:clr('bg', 'yellow'),
\         'airline_error': s:clr('bg', 'red'),
\       },
\     ),
\   'visual': s:color_map(
\       ['bg', 'yellow'],
\       ['white', 'brightblack'],
\       ['white', 'bg'],
\       {
\         'airline_warning': s:clr('bg', 'yellow'),
\         'airline_error': s:clr('bg', 'red'),
\       },
\     ),
\   'visual_modified': s:color_map(
\       ['bg', 'yellow'],
\       ['white', 'brightblack'],
\       ['white', 'bg'],
\       {
\         'airline_warning': s:clr('bg', 'yellow'),
\         'airline_error': s:clr('bg', 'red'),
\       },
\     ),
\   'inactive': s:color_map(
\       ['bg', 'brightblack'],
\       ['white', 'bg'],
\       ['white', 'bg'],
\       {
\         'airline_warning': s:clr('bg', 'yellow'),
\         'airline_error': s:clr('bg', 'red'),
\       },
\     ),
\   'terminal': s:color_map(
\       ['bg', 'green'],
\       ['white', 'brightblack'],
\       ['white', 'bg'],
\       {
\         'airline_term': s:clr('white', 'bg'),
\       },
\     ),
\}

" Extensions: {{{
" Tabline: {{{
if get(g:, 'airline#extensions#tabline#enabled', 0)
  let g:airline#themes#o#palette.tabline = {
        \ 'airline_tabfill': s:clr('bg', 'bg'),
        \
        \ 'airline_tab': s:clr('brightblack', 'bg'),
        \ 'airline_tabsel': s:clr('bg', 'cyan'),
        \ 'airline_tabmod': s:clr('bg', 'cyan', 'italic'),
        \
        \ 'airline_tab_right': s:clr('brightblack', 'bg'),
        \ 'airline_tabsel_right': s:clr('white', 'bg' ),
        \ 'airline_tabmod_right': s:clr('green', 'bg'),
        \}
endif
"}}}
" CtrlP: {{{2
if exists('g:loaded_ctrlp')
  let g:airline#themes#o#palette.ctrlp = airline#extensions#ctrlp#generate_color_map(
        \ s:clr('white', 'bg'),
        \ s:clr('white', 'brightblack'),
        \ s:clr('white', 'brightblue'),
        \)
endif
"}}}2
"}}}

" vim: fdm=marker ts=2 sts=2 sw=2 fdl=0:
